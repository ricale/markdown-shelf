# Place all the behaviors and hooks related to the matching controller here.
# All this logic will automatically be available in application.js.
# You can use CoffeeScript in this file: http://coffeescript.org/

#= require application
#= require jquery.history
#= require hmd/hmd
#= require hmd/hmd.ricaleinline

<% environment.context_class.instance_eval { include Rails.application.routes.url_helpers } %>

window.shelf = {} if typeof window.shelf is typeof undefined

window.shelf = (->
  isChanged = false
  confirmMessageToNotSave  = "저장되지 않았습니다. 계속 하시겠습니까?"
  confirmMessageWhenUnload = "저장되지 않은 데이터가 있습니다."
  notCategorizedText = "Not categorized"

  STATE =
    NEW:  1
    SHOW: 2

  #
  #
  # responsive

  resizeResponsive = ->
    $source = $("#source")
    $target = $("#target")
    $list   = $("#list")

    if $source.exists()
      boxHeight = $(window).height() - $source.offset().top - 15
      $source.css("height", boxHeight)
      $target.css("height", boxHeight)

      listHeight = $(window).height() - $list.offset().top - 15
      $list.css("height", listHeight)

    return

  resizeLayout = ->
    changeClass = (removingClass, addingClass) ->
      $editorContainer.changeClass(removingClass, addingClass)
      $previewContainer.changeClass(removingClass, addingClass)

    $sidebarContainer = $('#sidebar')
    $editorContainer  = $('#editor')
    $previewContainer = $('#preview')

    gridClasses = ['col-sm-5', 'col-sm-6', 'col-sm-10', 'col-sm-12']
    normalGridClass = 'col-sm-5'
    hiddenSidebarGridClass = 'col-sm-6'
    hiddenContentGridClass = 'col-sm-10'
    hiddenAllGridClass     = 'col-sm-12'

    sidebarIsVisible = $sidebarContainer.is(':visible')
    editorIsVisible  = $editorContainer.is(':visible')
    previewIsVisible = $previewContainer.is(':visible')

    if sidebarIsVisible
      if previewIsVisible and editorIsVisible
        addingGridClass = normalGridClass
      else
        addingGridClass = hiddenContentGridClass

    else
      if previewIsVisible and editorIsVisible
        addingGridClass = hiddenSidebarGridClass
      else
        addingGridClass = hiddenAllGridClass

    changeClass(gridClasses.join(" "), addingGridClass)

  toggleDirectory = ->
    $sidebar = $('#sidebar')
    if $(this).is(':checked') then $sidebar.show() else $sidebar.hide()

    resizeLayout()

  toggleContent = ($this, $container, $button) ->
    if $this.is(':checked')
      $container.show()
      $button.removeAttr('disabled')
    else
      $container.hide()
      $button.attr('disabled', 'disabled')

    resizeLayout()

  #
  #
  # hmd

  runHmd = ->
    hmd.run "#source", "#target"

  #
  #
  # writing

  requestNewWriting = (event) ->
    event.preventDefault()
    return false if !confirmToNotSave()

    History.pushState({state: STATE.NEW}, "Markdown-shelf", "/");

  responseNewWriting = ->
    $("#data_writing_id").val("0")
    $("#data_category_id").val("")
    $("#writing_title").text("New Document")
    $("#writing_category").text(notCategorizedText)

    $('#list .writing_selected').removeClass('writing_selected')

    $("#source").val("")
    $("#target").text("")
    togglePrivate(true)

    writingIsSaved()

  requestShowWriting = (event) ->
    event.preventDefault()
    return false if !confirmToNotSave()

    History.pushState({state: STATE.SHOW}, "Markdown-shelf", $(this).data('href'));

  responseShowWriting = (data) ->
    $('#source').val(data.content)
    $('#target').html(hmd.decode(data.content))
    $('#writing_title').html(data.title)
    $('#list .writing_selected').removeClass('writing_selected')
    $('#writing_'+data.id).addClass('writing_selected')

    $('#writing_category').html(data.category_name)

    $('#data_writing_id').val(data.id)
    $('#data_category_id').val(data.category_id)
    togglePrivate(data.private)

    writingIsSaved();

  saveWriting = (event) ->
    event.preventDefault()

    writingId  = $("#data_writing_id").val()
    categoryId = $("#data_category_id").val()
    title      = $("#writing_title").text()
    content    = $("#source").val()
    isPrivate  = getPrivate()

    if writingId == "0"
      url = "<%= writings_path %>"
      type = "POST"

    else
      url = "/writings/" + writingId
      type = "PUT"

    $.ajax
      url: url
      type: type
      data:
        writing:
          title: title
          category_id: categoryId
          content: content
          private: isPrivate
      dataType: "script"



  editTitleAndCategory = (event) ->
    if $('#data_writing_id').val() != ""
      $('#modal-edit-writing input[name="title"]').val($('#writing_title').text())
      $('#modal-edit-writing select[name="category_id"]').val($('#data_category_id').val())
      $('#modal-edit-writing').modal 'show'

  updateTitleAndCategory = (event) ->
    $('#writing_title').text($('#modal-edit-writing input[name="title"]').val())

    categoryId = $('#modal-edit-writing select[name="category_id"]').val()
    $('#data_category_id').val(categoryId)

    $categoryName = $('#writing_category')
    if categoryId == 0 or categoryId == ""
      $categoryName.text(notCategorizedText)
    else
      $categoryName.text($('#modal-edit-writing select[name="category_id"] option:selected').text())

    onChangeSource()

  #
  #
  # category

  newCategory = (event) ->
    event.preventDefault()
    if $('#data_writing_id').val() != ""
      $('#modal-new-category form').attr('action', "<%= categories_path %>")
      $('#modal-new-category input[name="category[name]"]').val("")
      $('#modal-new-category').modal 'show'

  editCategory = (event) ->
    event.preventDefault()
    if $('#data_writing_id').val() != ""
      $this = $(this)
      categoryId = $this.closest("li").attr('id').slice(9)
      categoryName = $this.closest("li").find('.category_info .name').text()

      $('#modal-edit-category form').attr('action', "<%= categories_path %>/" + categoryId)
      $('#modal-edit-category input[name="category[name]"]').val(categoryName)
      $('#modal-edit-category').modal 'show'

  #
  #
  # private flag

  changePrivate = (event) ->
    checkChanged(true)

  togglePrivate = (isPrivate) ->
    $privateCheckbox = $("#writing_private_flag")

    if isPrivate
      if !$privateCheckbox.is(':checked')
        $privateCheckbox.closest('.btn').click()
    else
      if $privateCheckbox.is(':checked')
        $privateCheckbox.closest('.btn').click()

  getPrivate = ->
    $('#writing_private_flag').is(':checked')

  #
  #
  # etc

  checkChanged = (changed) ->
    isChanged = changed

    if changed
      $('#save-light').changeClass('bg-success', 'bg-danger')
    else
      $('#save-light').changeClass('bg-danger', 'bg-success')


  confirmToNotSave = (event) ->
    if isChanged
      return confirm(confirmMessageToNotSave)
    else
      return true

  onChangeSource = (event) ->
    checkChanged(true)
    $('#source').unbind('input propertychange', onChangeSource)

  writingIsSaved = (event) ->
    checkChanged(false)
    $('#source').bind('input propertychange', onChangeSource)

  fadeOutCurrentMessage = ->
      $('.message .alert-custom').each ->
        $this = $(this)
        if $this.is(':visible')
          setTimeout ->
            $this.fadeOut(5000)
          , 1000

  #
  #
  # document ready event listener

  onReadyDocument = ->
    runHmd()
    resizeResponsive()

    $('#new_writing_button').click(requestNewWriting)
    $('#save_writing_button').click(saveWriting)
    $('#list').on('click', '.writing', requestShowWriting)
    $('#list').on('click', '.edit_category_button', editCategory)

    $('#new_category_button').click(newCategory)

    $('#source').bind('input propertychange', onChangeSource)

    $('#writing_title').click(editTitleAndCategory)
    $('#writing_category').click(editTitleAndCategory)

    $('#modal-edit-writing #btn-done').click(updateTitleAndCategory)

    $('#writing_private_flag').change(changePrivate)
    $('#toggle_directory').change(toggleDirectory)
    $('#toggle_editor').change ->
      toggleContent($(this), $('#editor'), $('#toggle_preview').closest('.btn'))
    $('#toggle_preview').change ->
      toggleContent($(this), $('#preview'), $('#toggle_editor').closest('.btn'))

    $('.default-tooltip').tooltip()

    fadeOutCurrentMessage()

  #
  #
  # statechange (hashchange) event listener

  History.Adapter.bind window, 'statechange', ->
    state = History.getState();

    switch(state.data.state)
      when STATE.NEW
        responseNewWriting()
      when STATE.SHOW
        $.ajax(
          url: state.url
          dataType: 'json'
        ).done responseShowWriting

  $(document).ready onReadyDocument
  $(document).on 'page:load', onReadyDocument

  $(window).load(resizeResponsive)
           .resize(resizeResponsive)
           .on 'beforeunload', (event) ->
    if isChanged
      return confirmMessageWhenUnload


  (->
    savedNow: ->
      writingIsSaved()
  )()
)()
